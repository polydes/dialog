plugins {
	id 'eclipse'
	id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).each {
	it.options.compilerArgs.add('--enable-preview')
}

//building an artifact using a local-only build artifact of Stencyl
var localBuild = project.hasProperty("local_build")
//developing as a submodule of Stencyl
var localDev = project.hasProperty("local_dev")

group = 'com.polydes'
version = rootProject.file('version.txt').text.trim()

sourceSets {
	main {
	    java {
            srcDir 'src'
        }
    }
}

processResources {
    from('res') {
    	into 'res/com/polydes/dialog'
    }
    from(rootProject.file('icon.png')) {
        into 'com/polydes/dialog/res'
    }
}

repositories {
	if(localBuild) {
		mavenLocal()
	}
	mavenCentral()
	maven {
		url "https://www.stencyl.com/dl/maven2/releases"
	}
	maven {
		url "https://www.stencyl.com/dl/maven2/snapshots"
	}
}

dependencies {
    //Stencyl and its dependencies
    implementation enforcedPlatform('com.stencyl:stencyl-platform:4.1.0-SNAPSHOT')
    if(localDev) {
		compileOnly project(":")
		compileOnly project(":stencyl-v5-core")
		compileOnly project(":stencyl-v5-toolset")
	} else {
		compileOnly group: 'com.stencyl', name: 'stencyl', version: '4.1.0-SNAPSHOT'
	}
	compileOnly group: 'com.stencyl',        name: 'jide-oss'
	compileOnly group: 'com.stencyl',        name: 'jide-components'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3'
	compileOnly group: 'ch.qos.reload4j',    name: 'reload4j'
    compileOnly group: 'commons-io',         name: 'commons-io'
    if(localDev) {
		compileOnly project(":ext-structures")
	} else if(localBuild) {
		compileOnly(files("../structures/engine/toolset-extension.jar"))
		//compileOnly(files(stencylWorkspace+"/engine-extensions/com.polydes.datastruct/toolset-extension.jar"))
	} else {
		//TODO: grab structures jar from repository
	}
}

jar {
	from {
		configurations.runtimeClasspath.collect { zipTree(it) }
	}
    manifest {
		attributes(
			"Extension-ID": "com.polydes.dialog",
			"Extension-Main-Class": "com.polydes.dialog.DialogExtension",
			"Extension-Version": version,
			"Extension-Icon": "com/polydes/dialog/res/icon.png",
			"Extension-Dependencies": "",
			"Extension-Type": "game",
			"Extension-Name": "Dialog Extension",
			"Extension-Description": "Toolset side of the Dialog Extension.",
			"Extension-Author": "Justin & Irock",
			"Extension-Website": "http://www.polydes.com/repo/toolset/com.polydes.dialog",
			"Extension-Repository": "http://www.polydes.com/repo",
			"Extension-Internal-Version": "6"
		)
	}
}

tasks.register('buildForEngine', Copy) {
	from(tasks.named("jar")) {
		rename { fileName -> "toolset-extension.jar" }
	}
	into "engine"
}

tasks.register('installToWorkspace', Sync) {
	dependsOn(":buildForEngine")
	from('engine')
	into stencylWorkspace+"/engine-extensions/com.polydes.dialog"
}