plugins {
	id 'eclipse'
	id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_20
    targetCompatibility = JavaVersion.VERSION_20
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

group = 'com.polydes'
version = rootProject.file('version.txt').text.trim()

sourceSets {
	main {
	    java {
            srcDir 'src'
        }
    }
}

processResources {
    from('res') {
    	into 'res/com/polydes/dialog'
    }
    from('icon.png') {
        into 'com/polydes/dialog/res'
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://www.stencyl.com/dl/maven2/releases"
    }
    maven {
        url "https://www.stencyl.com/dl/maven2/snapshots"
    }
}

dependencies {
    //Stencyl and its dependencies
    implementation enforcedPlatform('com.stencyl:stencyl-platform:4.1.0-SNAPSHOT')
    //compileOnly group: 'com.stencyl',        name: 'stencyl', version: '4.1.0-SNAPSHOT'
	compileOnly project(":")
	compileOnly project(":stencyl-v5-core")
	compileOnly project(":stencyl-v5-toolset")
    compileOnly group: 'com.stencyl',        name: 'jide-oss'
	compileOnly group: 'com.stencyl',        name: 'jide-components'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3'
	compileOnly group: 'ch.qos.reload4j',    name: 'reload4j'
    compileOnly group: 'commons-io',         name: 'commons-io'
    //compileOnly(files(stencylWorkspace+"/extensions/com.polydes.datastruct.jar"))
	compileOnly project(":ext-structures")
}

jar {
	from {
		configurations.runtimeClasspath.collect { zipTree(it) }
	}
    manifest {
		attributes(
			"Extension-ID": "com.polydes.dialog",
			"Extension-Main-Class": "com.polydes.dialog.DialogExtension",
			"Extension-Version": version,
			"Extension-Icon": "com/polydes/dialog/res/icon.png",
			"Extension-Dependencies": "stencyl-4.1.0-b10700,toolset-com.polydes.datastruct-2.0.0,engine-com.polydes.dialog-2.0.4",
			"Extension-Type": "game",
			"Extension-Name": "Dialog Extension",
			"Extension-Description": "Toolset side of the Dialog Extension.",
			"Extension-Author": "Justin & Irock",
			"Extension-Website": "http://www.polydes.com/repo/toolset/com.polydes.dialog",
			"Extension-Repository": "http://www.polydes.com/repo",
			"Extension-Internal-Version": "6"
		)
	}
}

tasks.register('installEngineToWorkspace', Sync) {
	from('engine')
	into stencylWorkspace+"/engine-extensions/com.polydes.dialog"
}

tasks.register('installToolsetToWorkspace', Copy) {
    from(tasks.named("jar"))
	into stencylWorkspace+"/extensions"
	rename { fileName -> "com.polydes.dialog.jar" }
}

tasks.register('installToWorkspace') {
	dependsOn 'installToolsetToWorkspace'
    dependsOn 'installEngineToWorkspace'
}